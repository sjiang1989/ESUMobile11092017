/*
 * Copyright 2015 Ellucian Company L.P. and its affiliates.
 */

apply plugin: 'com.android.application'

android {
    compileSdkVersion 22
    buildToolsVersion "22.0.1"

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 22
        versionName "4.0.0"
        versionCode 464
    }

    buildTypes {
        debug {
            pseudoLocalesEnabled true
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
        qa {
            applicationIdSuffix ".qa"
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }

    lintOptions {
        abortOnError false
    }

    productFlavors {
        ellucianGo {
            applicationId "com.ellucian.elluciango"
            versionName = getVersion(versionName)
        }
        customer {
            applicationId "edu.school.app"
            buildConfigField "String", "contentProvider", '"' + applicationId + '.contentprovider"'
            //noinspection GroovyAssignabilityCheck
            resValue "string", "content_provider_authority", '"' + applicationId + '.contentprovider"'
        }
    }
}

dependencies {
    compile 'com.google.code.gson:gson:2.3'
    compile 'com.google.android.gms:play-services:7.3.0'
    compile 'com.googlecode.android-query:android-query:0.25.9'
    compile 'com.android.support:support-v13:21.0.2'
    compile 'org.apache.commons:commons-io:1.3.2'
    compile 'org.jsoup:jsoup:1.8.1'
    compile 'com.android.support:appcompat-v7:22.1.1'
    compile 'com.android.support:cardview-v7:21.0.0'
    compile 'com.android.support:recyclerview-v7:21.0.0'
    compile 'com.android.support:support-v4:22.1.1'
}

def getVersion(version) {
    if(hasProperty('version_build')) {
        return version_build
    }
    return version
}

android.applicationVariants.all { variant ->
    if("ellucianGoRelease".equals(variant.name) || "ellucianGoDebug".equals(variant.name)) {
        buildConfigField "String", "contentProvider", '"com.ellucian.mobile.android.contentprovider"'
        resValue "string", "content_provider_authority", '"com.ellucian.mobile.android.contentprovider"'
    } else if("ellucianGoQa".equals(variant.name)) {
        buildConfigField "String", "contentProvider", '"com.ellucian.elluciango.qa.contentprovider"'
        resValue "string", "content_provider_authority", '"com.ellucian.elluciango.qa.contentprovider"'
    }
}


